group 'net.lazygun'
version '1.0.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'

sourceCompatibility = 1.8

ext {
    guiceVersion = '4.0'
    datanucleusVersion = '5.0.3'
    slf4jVersion = '1.7.12'
    spockVersion = '1.0-groovy-2.4'
    hikariVersion = '2.4.1'
    groovyVersion = '2.4.4'
    cglibVersion = '3.2.0'
    objenesisVersion = '2.1'
    guavaVersion = '18.0'
    h2Version = '1.4.192'
    javaslangVersion = '2.0.4'
    queryDslVersion = '4.1.4'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Use FindBugs annotations for code quality and static analysis (see http://findbugs.sourceforge.net/manual/annotations.html)
    compile "com.google.code.findbugs:annotations:3.0.1"

    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    compile "io.javaslang:javaslang:$javaslangVersion"
    compile "org.datanucleus:javax.jdo:3.2.0-m5"
    compile "com.google.guava:guava:$guavaVersion"

    //compile "com.querydsl:querydsl-jdo:$queryDslVersion"

    // Use Spock testing framework (http://docs.spockframework.org)
    // along with code-generation libraries for advanced mocking
    testCompile ("org.spockframework:spock-core:$spockVersion") {
        exclude module: 'groovy-all'
    }
    testRuntime "cglib:cglib-nodep:$cglibVersion"
    testRuntime "org.objenesis:objenesis:$objenesisVersion"

    testRuntime "org.datanucleus:datanucleus-core:5.0.3"
    testCompile "org.datanucleus:datanucleus-api-jdo:5.0.3"
    testRuntime "org.datanucleus:datanucleus-rdbms:5.0.3"
    testRuntime "com.h2database:h2:1.4.192"
}

task datanucleusEnhance {
    description "Enhance JDO model classes using DataNucleus Enhancer"
    dependsOn compileJava

    doLast {
        // define Ant task for DataNucleus Enhancer
        ant.taskdef(
            name : 'datanucleusenhancer',
            classpath : sourceSets.test.runtimeClasspath.asPath,
            classname : 'org.datanucleus.enhancer.EnhancerTask'
        )

        // run the DataNucleus Enhancer as an Ant task
        ant.datanucleusenhancer(
            classpath: sourceSets.test.runtimeClasspath.asPath,
            //verbose: true,
            api: "JDO",
            persistenceUnit: "Test"
        )
    }
}
datanucleusEnhance.mustRunAfter(classes, testClasses)
test.dependsOn datanucleusEnhance
jar.dependsOn datanucleusEnhance